cmpSingle:
;;Input: DE points to float1, HL points to float2
;;Output:
;;      float1 >= float2 : nc
;;      float1 <  float2 : c,nz
;;      float1 == float2 : z
;;  There is a margin of error allowed in the lower 2 bits of the mantissa.
    push hl
    push de
    push bc
    push af
    ld a,(de)
    sub (hl)
    ld c,a
    inc hl
    inc de
    ld a,(de)
    sbc a,(hl)
    ld b,a
    inc hl
    inc de

    ld a,(de)
    sbc a,(hl)
    push af
    inc hl
    inc de
    ld a,(de)
    sbc a,(hl)
    pop de          ;A,DBC
    ld e,a
    sbc a,a
    ld l,a
    
    jr nc,$+15
    xor a
    ld h,a \ sub c   \ ld c,a
    ld a,h \ sbc a,b \ ld b,a
    ld a,h \ sbc a,d \ ld d,a
    ld a,h \ sbc a,e \ ld e,a
    
    call chk    ;returns flags
    rl l
    inc a
    dec a
    pop hl
    ld a,h
    pop bc
    pop de
    pop hl
    ret
chk:
    ld a,e
    or a
    ret nz
    or d
    ret nz
    or b
    ret nz
    ld a,c
    add a,-4
    sbc a,a
    ret

.echo "cmpSingle:",$-cmpSingle