#define var_c fpOP1   ;input
#define var_y var_c+4 ;  used for sqrt32
#define var_x fpOP2   ;output
#define var_b fpOP2   ; 4 bytes, result gets copied to bottom anyways
#define var_a fpOP2   ;   2 bytes
#define var_z0 fpOP2+8;used in sqr32

xsqrt:
;HL points to x
;BC points to the output
;computes x^.5, if x>=0
;speed: 388+mov8+mov10+sqrt64+{0,7+srlfpOP1_mantissa}
;avg: 6540.792cc

  push hl
  push de
  push bc
  push af
  push ix
  push bc
  call +_
  ld hl,var_x
  pop de
  call mov8
  ld hl,(fpOP1+8)
  ex de,hl
  ld (hl),e
  inc hl
  ld (hl),d
  pop ix
  pop af
  pop bc
  pop de
  pop hl
  ret
_:
  ld de,fpOP1
  call mov10
  ex de,hl
  dec hl
  ld a,(hl)
  or a
  jp m,sqrtNAN
  ld d,a
  dec hl
  ld e,(hl)
  or e
  jp z,sqrt_special
  ld a,d
  add a,$40
  rra
  ld h,a
  ld a,e
  rra
  ld l,a
  ld (fpOP1+8),hl
  call nc,srlfpOP1_mantissa
#include "../extended/sqrt/sqrt64.z80"
sqrt_special:
;special case: 0 -> 0, NaN -> NaN, +inf -> +inf, so output=input
  ld de,fpOP1
  ld hl,var_x
  jp mov10
sqrtNAN:
  ld hl,const_NaN
  ld de,fpOP1
  jp mov10
.echo "xsqrt: ",$-xsqrt
#include "../extended/sqrt/sqrt32.z80"
.echo "  sqrt32: +",$-sqrt32
#include "../extended/sqrt/sqrt16.z80"
.echo "  sqrt16: +",$-sqrtHL

#undefine var_b fpOP2   ; 4 bytes, result gets copied to bottom anyways
#undefine var_a fpOP2   ;   2 bytes
#undefine var_c fpOP2+8 ;input
#undefine var_y var_c+4 ;  used for sqrt32
#undefine var_z0 fpOP2+16
