xOP1mul10:
;Note! Does not check for special numbers! Does not check overflow!
  ld hl,xOP1
  ld de,xOP2
  call mov8
  ; ld hl,xOP1+8 ; useless thanks to the 8x LDI's just before...
  ld a,3
  add a,(hl)
  ld (hl),a
  jr nc,+_
  inc l
  inc (hl)
_:
#if ((xOP2+7)>>8)==((xOP1+8)>>8)
  ld l,255&(xOP2+7)
#else
  ld hl,xOP2+7
#endif
  srl (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl)
#if ((xOP2+7)>>8)==(xOP2>>8)
  ld l,255&(xOP2+7)
#else
  ld hl,xOP2+7
#endif
  srl (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl)
  ld de,xOP1
  call adc64
  ret nc
#if ((xOP1+8)>>8)==(xOP2>>8)
  ld l,255&(xOP1+8)
#else
  ld hl,xOP1+8
#endif
  inc (hl)
  jr nz,+_
  inc l
  inc (hl)
_:
  ex de,hl
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl) \ dec l
  rr (hl)
  ret
